import topology.instances.real
import analysis.normed_space.banach_steinhaus

open set filter
open_locale topological_space filter


variables {X : Type*} [metric_space X] (a b c : X)

#check (dist a b : â„)

#check (dist_nonneg : 0 â‰¤ dist a b)

#check (dist_eq_zero : dist a b  = 0 â†” a = b)

#check (dist_comm a b : dist a b  = dist b a)

#check (dist_triangle a b c : dist a c â‰¤ dist a b + dist b c)


-- Note the next three lines are not quoted, their purpose is to make sure those things don't get renamed while we're looking elsewhere.
#check emetric_space
#check pseudo_metric_space
#check pseudo_emetric_space



example {u : â„• â†’ X} {a : X} :
  tendsto u at_top (ð“ a) â†” âˆ€ Îµ > 0, âˆƒ N, âˆ€ n â‰¥ N, dist (u n) a < Îµ :=
metric.tendsto_at_top

example {X Y : Type*} [metric_space X] [metric_space Y] {f : X â†’ Y} :
  continuous f â†”
  âˆ€ x : X, âˆ€ Îµ > 0, âˆƒ Î´ > 0, âˆ€ x', dist x' x < Î´ â†’ dist (f x') (f x) < Îµ :=
metric.continuous_iff



example {X Y : Type*} [metric_space X] [metric_space Y] {f : X â†’ Y} (hf : continuous f) :
  continuous (Î» p : X Ã— X, dist (f p.1) (f p.2)) :=
by continuity


example {X Y : Type*} [metric_space X] [metric_space Y] {f : X â†’ Y} (hf : continuous f) :
  continuous (Î» p : X Ã— X, dist (f p.1) (f p.2)) :=
continuous_dist.comp ((hf.comp continuous_fst).prod_mk (hf.comp continuous_snd))




example {X Y : Type*} [metric_space X] [metric_space Y] {f : X â†’ Y} (hf : continuous f) :
  continuous (Î» p : X Ã— X, dist (f p.1) (f p.2)) :=
begin
  apply continuous.dist,
  exact hf.comp continuous_fst,
  exact hf.comp continuous_snd
end

example {X Y : Type*} [metric_space X] [metric_space Y] {f : X â†’ Y} (hf : continuous f) :
  continuous (Î» p : X Ã— X, dist (f p.1) (f p.2)) :=
(hf.comp continuous_fst).dist (hf.comp continuous_snd)



example {X Y : Type*} [metric_space X] [metric_space Y] {f : X â†’ Y} (hf : continuous f) :
  continuous (Î» p : X Ã— X, dist (f p.1) (f p.2)) :=
hf.fst'.dist hf.snd'



example {f : â„ â†’ X} (hf : continuous f) : continuous (Î» x : â„, f (x^2 + x)) :=
begin
  apply hf.comp,
  apply continuous.add,
  { apply continuous_pow },
  exact continuous_id
end

example {f : â„ â†’ X} (hf : continuous f) : continuous (Î» x : â„, f (x^2 + x)) :=
hf.comp ((continuous_pow _).add continuous_id)


example {X Y : Type*} [metric_space X] [metric_space Y] (f : X â†’ Y) (a : X) :
continuous_at f a â†” âˆ€ Îµ > 0, âˆƒ Î´ > 0, âˆ€ {x}, dist x a < Î´ â†’ dist (f x) (f a) < Îµ :=
metric.continuous_at_iff



variables r : â„

example : metric.ball a r = {b | dist b a < r} := rfl

example : metric.closed_ball a r = {b | dist b a â‰¤ r} := rfl



example (hr : 0 < r) : a âˆˆ metric.ball a r := metric.mem_ball_self hr

example (hr : 0 â‰¤ r) : a âˆˆ metric.closed_ball a r := metric.mem_closed_ball_self hr


example (s : set X) : is_open s â†” âˆ€ x âˆˆ s, âˆƒ Îµ > 0, metric.ball x Îµ âŠ† s :=
metric.is_open_iff



example {s : set X} : is_closed s â†” is_open sá¶œ :=
is_open_compl_iff.symm

example {s : set X} (hs : is_closed s) {u : â„• â†’ X} (hu : tendsto u at_top (ð“ a))
  (hus : âˆ€ n, u n âˆˆ s) : a âˆˆ s :=
hs.mem_of_tendsto hu (eventually_of_forall hus)

example {s : set X} : a âˆˆ closure s â†” âˆ€ Îµ > 0, âˆƒ b âˆˆ s, a âˆˆ metric.ball b Îµ :=
metric.mem_closure_iff


example {u : â„• â†’ X} (hu : tendsto u at_top (ð“ a)) {s : set X} (hs : âˆ€ n, u n âˆˆ s) :
  a âˆˆ closure s :=
begin
  rw metric.mem_closure_iff,
  rw metric.tendsto_at_top at hu,
  intros Îµ Îµpos,
  cases hu Îµ Îµpos with N hN,
  use [u N, hs N],
  rw dist_comm,
  apply hN,
  exact le_rfl
end



example {x : X} {s : set X} : s âˆˆ ð“ x â†” âˆƒ Îµ > 0, metric.ball x Îµ âŠ† s :=
metric.nhds_basis_ball.mem_iff

example {x : X} {s : set X} : s âˆˆ ð“ x â†” âˆƒ Îµ > 0, metric.closed_ball x Îµ âŠ† s :=
metric.nhds_basis_closed_ball.mem_iff




example : is_compact (set.Icc 0 1 : set â„) :=
is_compact_Icc

example {s : set X} (hs : is_compact s) {u : â„• â†’ X} (hu : âˆ€ n, u n âˆˆ s) :
  âˆƒ a âˆˆ s, âˆƒ Ï† : â„• â†’ â„•, strict_mono Ï† âˆ§ tendsto (u âˆ˜ Ï†) at_top (ð“ a) :=
hs.tendsto_subseq hu

example {s : set X} (hs : is_compact s) (hs' : s.nonempty)
  {f : X â†’ â„} (hfs : continuous_on f s) :
  âˆƒ x âˆˆ s, âˆ€ y âˆˆ s, f x â‰¤ f y :=
hs.exists_forall_le hs' hfs

example {s : set X} (hs : is_compact s) (hs' : s.nonempty)
  {f : X â†’ â„} (hfs : continuous_on f s) :
  âˆƒ x âˆˆ s, âˆ€ y âˆˆ s, f y â‰¤ f x :=
hs.exists_forall_ge hs' hfs

example {s : set X} (hs : is_compact s) : is_closed s :=
hs.is_closed



example {X : Type*} [metric_space X] [compact_space X] : is_compact (univ : set X) :=
compact_univ


#check is_compact.is_closed


example {X : Type*} [metric_space X] {Y : Type*} [metric_space Y] {f : X â†’ Y} :
  uniform_continuous f â†” âˆ€ Îµ > 0, âˆƒ Î´ > 0, âˆ€ {a b : X}, dist a b < Î´ â†’ dist (f a) (f b) < Îµ :=
metric.uniform_continuous_iff



example {X : Type*} [metric_space X] [compact_space X] {Y : Type*} [metric_space Y]
  {f : X â†’ Y} (hf : continuous f) : uniform_continuous f :=
begin
  rw metric.uniform_continuous_iff,
  intros Îµ Îµpos,
  let Ï† : X Ã— X â†’ â„ := Î» p, dist (f p.1) (f p.2),
  let K := {p : X Ã— X | Îµ â‰¤ Ï† p},
  have cont_Ï† : continuous Ï† := (hf.comp continuous_fst).dist (hf.comp continuous_snd),
  have comp_K : is_compact K := (is_closed_le continuous_const cont_Ï†).is_compact,
  cases eq_empty_or_nonempty K with hK,
  { use [1, one_pos],
    intros a b _,
    apply lt_of_not_le,
    change (a, b) âˆ‰ K,
    rw hK,
    apply not_mem_empty },
  rcases comp_K.exists_forall_le h continuous_dist.continuous_on with âŸ¨âŸ¨xâ‚€, xâ‚âŸ©, xx_mem, xx_infâŸ©,
  use dist xâ‚€ xâ‚,
  split,
  { apply dist_pos.mpr,
    rintro rfl,
    dsimp [Ï†] at xx_mem,
    rw dist_self at xx_mem,
    linarith },
  intros a b hlt,
  apply lt_of_not_le,
  change (a, b) âˆ‰ K,
  intro ab_mem,
  have := xx_inf (a, b) ab_mem,
  linarith
end




example (u : â„• â†’ X) : cauchy_seq u â†” âˆ€ Îµ > 0, âˆƒ N : â„•, âˆ€ m â‰¥ N,  âˆ€ n â‰¥ N, dist (u m) (u n) < Îµ :=
metric.cauchy_seq_iff

example (u : â„• â†’ X) : cauchy_seq u â†” âˆ€ Îµ > 0, âˆƒ N : â„•, âˆ€ n â‰¥ N, dist (u n) (u N) < Îµ :=
metric.cauchy_seq_iff'


example [complete_space X] (u : â„• â†’ X) (hu : cauchy_seq u) : âˆƒ x, tendsto u at_top (ð“ x) :=
cauchy_seq_tendsto_of_complete hu



open_locale big_operators
open finset


lemma cauchy_seq_of_le_geometric_two' {u : â„• â†’ X} (hu : âˆ€ (n : â„•), dist (u n) (u (n + 1)) â‰¤ (1 / 2) ^ n) :
  cauchy_seq u :=
begin
  rw metric.cauchy_seq_iff',
  intros Îµ Îµ_pos,
  obtain âŸ¨N, hNâŸ© : âˆƒ N : â„•, 1 / 2 ^ N * 2 < Îµ,
  { have half_nonneg : (0 : â„) â‰¤ 1 / 2, by norm_num,
    have half_lt_one : 1 / 2 < (1 : â„), by norm_num,
    have half_pow_vanishes := tendsto_pow_at_top_nhds_0_of_lt_1 half_nonneg half_lt_one,
    rw metric.tendsto_at_top at half_pow_vanishes,
    cases half_pow_vanishes (Îµ / 2) (half_pos Îµ_pos) with N hN,
    use N,
    have := hN N le_rfl,
    rw real.dist_0_eq_abs at this,
    have half_pow_pos : (0 : â„) < (1 / 2) ^ N, { apply pow_pos, norm_num },
    rw abs_of_pos half_pow_pos at this,
    rw â† one_div_pow,
    linarith },
  use N,
  intros n hn,
  obtain âŸ¨k, rfl : n = N + kâŸ© := le_iff_exists_add.mp hn,
  calc dist (u (N + k)) (u N) = dist (u (N+0)) (u (N + k)) : by { rw dist_comm, rw add_zero }
  ... â‰¤ âˆ‘ i in range k, dist (u (N + i)) (u (N + (i + 1))) : dist_le_range_sum_dist (Î» n, u (N + n)) k
  ... â‰¤ âˆ‘ i in range k, (1/2 : â„)^(N+i) : by { apply sum_le_sum,
                                               intros n _,
                                               apply hu }
  ... = 1/2^N*âˆ‘ i in range k, (1 / 2) ^ i : by { rw mul_sum,
                                                 congr, ext,
                                                 rw [pow_add, one_div_pow], }
  ... â‰¤ 1/2^N*2 : by { apply (mul_le_mul_left _).mpr,
                       { apply sum_geometric_two_le },
                       rw â† one_div_pow, apply pow_pos, norm_num }
  ... < Îµ : hN
end


open metric

example [complete_space X] (f : â„• â†’ set X) (ho : âˆ€ n, is_open (f n)) (hd : âˆ€ n, dense (f n)) : dense (â‹‚n, f n) :=
begin
  let B : â„• â†’ â„ := Î» n, (1/2)^n,
  have Bpos : âˆ€ n, 0 < B n, by simp,
  /- Translate the density assumption into two functions `center` and `radius` associating
  to any n, x, Î´, Î´pos a center and a positive radius such that
  `closed_ball center radius` is included both in `f n` and in `closed_ball x Î´`.
  We can also require `radius â‰¤ (1/2)^(n+1)`, to ensure we get a Cauchy sequence later. -/
  have : âˆ€ (n : â„•)  (x : X) (Î´ > 0), âˆƒ (y : X) (r > 0), r â‰¤ B (n+1) âˆ§ closed_ball y r âŠ† (closed_ball x Î´) âˆ© f n,
  { intros n x Î´ Î´pos,
    rcases metric.mem_closure_iff.mp (hd n x) (Î´ / 2) (half_pos Î´pos) with âŸ¨y, ymem, dxyâŸ©,
    use y,
    rcases is_open_iff.mp (ho n) y ymem with âŸ¨Îµ, Îµpos, byÎµâŸ©,
    let r := min (Î´ / 2) (min (Îµ / 2) (B (n + 1))),
    use [r, by { simp [(half_pos Î´pos), (half_pos Îµpos)]}, by simp],
    apply set.subset_inter,
    { apply closed_ball_subset_closed_ball',
      rw dist_comm,
      apply le_of_lt,
      calc r + dist x y < r + Î´ / 2     : by { apply add_lt_add_left, exact dxy }
           ...          â‰¤ Î´ / 2 + Î´ / 2 : by { apply add_le_add_right, simp }
           ...          = Î´             : add_halves Î´ },
    apply subset_trans _ byÎµ,
    apply closed_ball_subset_ball,
    calc r   â‰¤ Îµ / 2 : by simp
         ... < Îµ     : half_lt_self Îµpos },
  choose! center radius Hpos HB Hball using this,
  intros x,
  rw mem_closure_iff_nhds_basis nhds_basis_closed_ball,
  intros Îµ Îµpos,
  /- `Îµ` is positive. We have to find a point in the ball of radius `Îµ` around `x` belonging to all
  `f n`. For this, we construct inductively a sequence `F n = (c n, r n)` such that the closed ball
  `closed_ball (c n) (r n)` is included in the previous ball and in `f n`, and such that
  `r n` is small enough to ensure that `c n` is a Cauchy sequence. Then `c n` converges to a
  limit which belongs to all the `f n`. -/
  let F : â„• â†’ (X Ã— â„) := Î»n, nat.rec_on n (prod.mk x (min Îµ (B 0)))
                              (Î»n p, prod.mk (center n p.1 p.2) (radius n p.1 p.2)),
  let c : â„• â†’ X := Î» n, (F n).1,
  let r : â„• â†’ â„ := Î» n, (F n).2,
  have rpos : âˆ€ n, 0 < r n,
  { intro n, induction n with n ih,
    { exact lt_min Îµpos (Bpos 0) },
    exact Hpos n (c n) (r n) ih },
  have rB : âˆ€n, r n â‰¤ B n,
  { intro n, cases n,
    { apply min_le_right },
    exact HB n (c n) (r n) (rpos n) },
  have incl : âˆ€n, closed_ball (c (n+1)) (r (n+1)) âŠ† (closed_ball (c n) (r n)) âˆ© (f n),
  { intro n,
    apply Hball n (c n) (r n) (rpos n) },
  have cdist : âˆ€ n, dist (c n) (c (n+1)) â‰¤ B n,
  { intro n,
    rw dist_comm,
    change c (n+1) âˆˆ closed_ball (c n) (B n),
    apply mem_of_mem_of_subset,
    exact mem_of_mem_inter_left (incl n (mem_closed_ball_self (rpos (n+1)).le)),
    apply closed_ball_subset_closed_ball,
    exact rB n },
  have : cauchy_seq c, from cauchy_seq_of_le_geometric_two' cdist,
  -- as the sequence `c n` is Cauchy in a complete space, it converges to a limit `y`.
  rcases cauchy_seq_tendsto_of_complete this with âŸ¨y, ylimâŸ©,
  -- this point `y` will be the desired point. We will check that it belongs to all
  -- `f n` and to `ball x Îµ`.
  use y,
  have I : âˆ€n, âˆ€ m â‰¥ n, closed_ball (c m) (r m) âŠ† closed_ball (c n) (r n),
  { intros n m nlem,
    obtain âŸ¨k, rfl : m = n + kâŸ© := le_iff_exists_add.mp nlem,
    induction k with k ih,
    { refl },
    transitivity closed_ball (c (n + k)) (r (n + k)),
    { exact (set.subset_inter_iff.mp (incl (n + k))).left },
    exact ih (n.le_add_right k) },
  have yball : âˆ€n, y âˆˆ closed_ball (c n) (r n),
  { intro n,
    apply is_closed_ball.mem_of_tendsto ylim,
    rw eventually_iff_exists_mem,
    use [Ici n, Ici_mem_at_top n],
    intros m mgen, dsimp at mgen,
    apply I n m mgen,
    exact mem_closed_ball_self (rpos m).le },
  split,
  { apply mem_Inter_of_mem,
    intro n,
    have ymem := Hball n (c n) (r n) (rpos n) (yball (n + 1)),
    rw mem_inter_iff at ymem,
    exact ymem.right },
  have rÎµ : r 0 â‰¤ Îµ, from min_le_left _ _,
  apply closed_ball_subset_closed_ball rÎµ,
  exact yball 0
end



